+++
date = '2019-04-21'
title = 'ocaml ppx'
tags = ['ocaml']
+++

rust con 上有两个主题都涉及了 rust 的 procedural macro。
想起了 ocaml 的 ppx，想到了，那就写一写。

---

[deriving](https://github.com/ocaml-ppx/ppx_deriving) 是比较常用的一个 ppx 库。
比如下面这个例子，会 rust 的看着像 rust，会 haskell 的看着像 haskell。
（大概，没有人是先会 ocaml 的 😂

```ocaml
type point3d = float * float * float
[@@deriving show]
```

那么，这个 `@@deriving` 是怎么实现的呢？
官方文档里只有寥寥数语，[Attributes](https://caml.inria.fr/pub/docs/manual-ocaml-4.07/extn.html#sec260)、[Extensions](https://caml.inria.fr/pub/docs/manual-ocaml-4.07/extn.html#sec262)，这个文档，大概没人能看明白……
这种时候，就只能靠社区写的工具和教程了……

其实 ppx/macro 也还是一个函数，输入 ast 输出 ast，写之前先想明白要做什么就可以了。
调试的话，最粗暴的就是打印看下是否符合预期，不过好像也没有不粗暴的方法？
比如前面的例子，输出的代码是这样的

```ocaml
(* ocamlfind ocamlc -dsource -linkpkg -package ppx_deriving.std point3d.ml *)
type point3d = float * float * float [@@deriving show]

let rec (pp_point3d : Format.formatter -> point3d -> Ppx_deriving_runtime.unit)
    =
    (let open! Ppx_deriving_runtime in
     fun fmt (a0, a1, a2) ->
         Format.fprintf fmt "(@[";
         (Format.fprintf fmt "%F") a0;
         Format.fprintf fmt ",@ ";
         (Format.fprintf fmt "%F") a1;
         Format.fprintf fmt ",@ ";
         (Format.fprintf fmt "%F") a2;
         Format.fprintf fmt "@])") [@ocaml.warning "-A"]


and show_point3d : point3d -> Ppx_deriving_runtime.string =
    fun x -> Format.asprintf "%a" pp_point3d x
```

---

学 ppx 入门例子是 `ppx_getenv`，从 [硬怼 AST](https://whitequark.org/blog/2014/04/16/a-guide-to-extension-points-in-ocaml/) 到 [社区沉淀了一些工具](http://rgrinberg.com/posts/extension-points-3-years-later/) ，再到工具整合成 [ppxlib](https://github.com/ocaml-ppx/ppxlib/blob/master/HISTORY.md)。
ppxlib 确实将匹配过程简化了很多，但是 ast_mapper 可以照着 `ocamlc -dparsetree` 写，ppxlib 的 API 则让人懵逼……

`ppx_getenv` 会将 `let user = [%getenv "USER"]` 改写成 `let user = Some("h11")`，在编译时计算了 `Sys.getenv_opt "USER"`。
可以通过 `ocamlc -dparsetree get-user-example.ml` 得到要处理的语法树，然后照着 [Parsetree](https://caml.inria.fr/pub/docs/manual-ocaml-4.07/libref/Parsetree.html) 把输入的语法树写下来。
比如 `[%getenv ...]` 是 `Pexp_extension`，参数是 `Pexp_constant PConst_string`。

```ocaml
open Asttypes
open Parsetree

let build_exp loc env =
    match Sys.getenv_opt env with
        | None ->
            let lid = { txt= Longident.parse "None"; loc } in
            Ast_helper.Exp.construct lid None
        | Some e ->
            let exp = Ast_helper.Exp.constant (Pconst_string (e, None)) in
            let lid = { txt= Longident.parse "Some"; loc } in
            Ast_helper.Exp.construct lid (Some exp)


let getenv_translate loc = function
    (* match `"USER"`  *)
    | PStr
            [ { pstr_desc=
                    Pstr_eval
                        ({ pexp_desc= Pexp_constant (Pconst_string (env, None)); _ }, _)
              ; _
              }
            ] ->
        build_exp loc env
    | _ ->
        raise
            (Location.Error
                 (Location.error
                      ~loc
                      "[%getenv] accepts a string, e.g. [%getenv \"USER\"]"))


let ppx_mapper _argv =
    let open Ast_mapper in
    let expr_mapper mapper expr =
        (* match `[%getenv ...]`  *)
        match expr.pexp_desc with
            | Pexp_extension ({ txt= "getenv"; loc }, payload) ->
                getenv_translate loc payload
            | _ -> default_mapper.expr mapper expr
    in
    { default_mapper with expr= expr_mapper }


let () = Ast_mapper.register "getenv" ppx_mapper
```

之后把 ppx 编译好就能使用了

```
$ ocamlfind ocamlc -linkpkg -package compiler-libs.common getenv.ml -o ppx_getenv
$ ocamlc get-user-example.ml -ppx './getenv.ppx' -dsource
```

整个过程由 `Ast_mapper` 驱动，匹配 `Parsetree`，再通过 `Ast_helper` 构造返回值。全程手动构造 AST，确实不爽。
如果只是写 extension，那么直接 ppxlib 也挺好。但是怎么用 ppxlib 写 attribute，我是没看明白……

---

回到最初的例子，`deriving_eq` 要怎么实现呢？
首先，输入是类型申明，输出是 `eq_type-name` 函数。
（好了好了，ad hoc polymorphism 没有就没有了，不要喷了。

其次，`deriving_eq` 是放在类型申明后面，所以要实现的是 `item-attribute`。
不知道什么是 `item-attribute`？官方文档。
简单讲，`extension` 是语法上的占位符，比如前面用 `[%getenv "USER"]` 代替 `Some(user)`，就是 `extension` 代替了 [`expr`](https://caml.inria.fr/pub/docs/manual-ocaml-4.07/expr.html#expr)。
而 `attribute` 类似注解，修饰原本完整的语句。`type point3d = float * float * float` 本身是完整的，要做的只是加上标识，方便之后做展开。
这点看 `Parsetree` 可能更好理解，比如 `type expression = { expression_desc; attributes }`。这里 expression_desc 可以是一个 extension，而 attributes 则可以是空数组。

假设输入是 `type point3d = float * float * float [@@deriving_eq]`，一样先观察 `ocamlc -dparsetree point3d.ml` 的输出。
我们要处理的是带 `attribute "deriving_eq"` 的 `type_declaration`，之后根据 `type_declaration` 里具体的属性实现 `eq_type_name`，插入到源代码里。
想一想要手写 eq 函数的 AST 就觉得可怕，type_declaration 的 deriving 还是去写 ppx_deriving plugin 吧……

嗯，坑了……

---

更新一下，`ppx_tools` 不错
- `ocamlfind ppx_tools/dumpast` 的输出比 `ocamlc -dparsetree` 更直观，直接就是 Parsetree 的结构
- 有了 `ppx_tools.metaquot`，生成 AST 也会简单一些

前面的 getenv 可以写成这样

```ocaml
let build_exp env =
    match Sys.getenv_opt env with
        | None -> [%expr None]
        | Some e ->
            let ee = Ast_convenience.str e in
            [%expr Some [%e ee]]
```

然后 `ocamlfind ocamlc -linkpkg -package compiler-libs.common,ppx_tools.metaquot,ppx_tools getenv.ml -o ppx_getenv` 编译。
不过还是那句，单纯的 extension 还是上 ppxlib 吧。

---

再更新一下，`ppx_tools/dumpast` 缺了类型，还是要配合 `ocamlc -dparsetree`。

另外，前面的 deriving_eq 不该处理 `type_declaration`。
根据 mapper 的签名 `type_declaration : mapper -> Parsetree.type_declaration -> Parsetree.type_declaration`，如果只处理 `type_declaration` 就不能增加 `eq_type_name` 函数了。
所以要处理的是上一级的 `structure_item`，再在里面分析 `Pstr_type`，新增 `Pstr_eval`，实现插入函数的目的。

ppx_tools/ppxlib 都不怎么会用……，还是先硬怼 AST，再用工具增强代码的可读性……
